plugins {
    id 'com.android.application'
    id 'com.google.protobuf' // proto
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.12.0'
    } // 相当于proto编译器
    plugins {
        grpc { artifact = 'io.grpc:protoc-gen-grpc-java:1.33.0' } // Grpc单独的编译器

        javalite {
            artifact = 'com.google.protobuf:protoc-gen-javalite:3.0.0' // 官方推荐的方法，Android 适用javalite,相较于java插件，生成的代码更轻量化
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java { option 'lite' }
            }
            task.plugins {
                grpc { // Options added to --grpc_out
                    option 'lite' }
            }
        }
    }
    generatedFilesBaseDir = "$projectDir/src/generated"
}

android {
    compileSdk 33

    defaultConfig {
        applicationId "com.zgg2001.projectz"
        minSdk 21
        targetSdk 33
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildFeatures {
        viewBinding true
    }

    sourceSets {
        main {
            java {
                srcDir 'src/main/java'
            }
            proto {
                srcDir 'src/main/proto'
                include '*.proto'
            }
        }
        test {
            proto {
                // In addition to the default 'src/test/proto'
                srcDir 'src/main/proto'
            }
        }
    }
}

dependencies {

    //#####注意这里#####
    //implementation group:'com.google.protobuf', name:'protobuf-java', version:'3.12.0'

    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'androidx.annotation:annotation:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.3.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.1'
    implementation 'io.grpc:grpc-okhttp:1.33.0'
    implementation 'io.grpc:grpc-protobuf-lite:1.33.0'
    implementation 'io.grpc:grpc-stub:1.33.0'
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}